project(
  'ghex', 'c',
  version: '4.alpha.1',
  meson_version: '>=0.53.0',
  license: 'GPL2'
)

if get_option('development')
  app_id = 'org.gnome.GHex.Devel'
  resource_base_path = '/org/gnome/GHex/Devel'
else
  app_id = 'org.gnome.GHex'
  resource_base_path = '/org/gnome/GHex'
endif

version_arr = meson.project_version().split('.')
ghex_version_major = version_arr[0].to_int()

libghex_version_major = version_arr[0].to_int()

ghex_prefix = get_option('prefix')
ghex_libdir = join_paths(ghex_prefix, get_option('libdir'))
ghex_includedir = join_paths(ghex_prefix, get_option('includedir'))
ghex_datadir = join_paths(ghex_prefix, get_option('datadir'))
ghex_localedir = join_paths(ghex_prefix, get_option('localedir'))
ghex_pkgconfigdir = join_paths(ghex_libdir, 'pkgconfig')
ghex_applicationsdir = join_paths(ghex_datadir, 'applications')
ghex_schemasdir = join_paths(ghex_datadir, 'glib-2.0/schemas')
ghex_appdatadir = join_paths(ghex_datadir, 'metainfo')
ghex_iconsdir = join_paths(ghex_datadir, 'icons')

ghex_root_dir = include_directories('.')
ghex_po_dir = join_paths(meson.source_root(), 'po')

# warning cflags
warning_cflags = [
  '-Wno-unused-variable',
  '-Wno-unused-parameter'
]
c_compiler = meson.get_compiler('c')
supported_warning_cflags = c_compiler.get_supported_arguments(warning_cflags)
add_global_arguments(supported_warning_cflags, language : 'c')

gnome = import('gnome')
i18n = import('i18n')

cc = meson.get_compiler('c')

# just to avoid manually punching it into the XML files as well as the source.
shaded_box_max = 1000

config_h = configuration_data()
config_h.set_quoted('APP_ID', app_id)
config_h.set_quoted('RESOURCE_BASE_PATH', resource_base_path)
config_h.set_quoted('PACKAGE_NAME', meson.project_name())
config_h.set_quoted('PACKAGE_VERSION', meson.project_version())
config_h.set_quoted('PACKAGE_STRING', '@0@ @1@'.format(meson.project_name(), meson.project_version()))
config_h.set_quoted('PACKAGE_DATADIR', ghex_datadir)
config_h.set_quoted('PACKAGE_LIBDIR', ghex_libdir)
config_h.set_quoted('PACKAGE_LOCALE_DIR', ghex_localedir)

config_h.set('VERSION', 'PACKAGE_VERSION')
config_h.set('GETTEXT_PACKAGE', 'PACKAGE_NAME')
config_h.set('LOCALEDIR', 'PACKAGE_LOCALE_DIR')

config_h.set('CONFIG_H_SHADED_BOX_MAX', shaded_box_max)

config_h.set_quoted('LIBGTKHEX_RELEASE_STRING', 'gtkhex-@0@.0'.format(libghex_version_major))

config_h.set('DEBUG', get_option('debug'))

# Always enable; it's generated by Meson anyway.
config_h.set('HAVE_CONFIG_H', true)

config_h.set10('ENABLE_NLS', true) # Always enabled

check_headers = [
  'dlfcn.h',
  'inttypes.h',
  'memory.h',
  'stdint.h',
  'stdlib.h',
  'string.h',
  'sys/stat.h',
  'sys/types.h',
  'unistd.h'
]

foreach h : check_headers
  if cc.has_header(h)
    config_h.set('HAVE_' + h.underscorify().to_upper(), 1)
  endif
endforeach

check_functions = [
  'pow',
  'strstr',
  'strtoul'
]

foreach f : check_functions
  if cc.has_function(f)
    config_h.set('HAVE_' + f.underscorify().to_upper(), 1)
  endif
endforeach

gio_dep = dependency('gio-2.0', version: '>= 2.66.0')
gtk_dep = dependency('gtk4', version: '>= 4.0.0')

configure_file(
  output: 'config.h',
  configuration: config_h
)

update_icon_cache_prg = find_program('gtk-update-icon-cache', required : false)
update_desktop_database_prg = find_program('update-desktop-database', required : false)
compile_schemas_prg = find_program('glib-compile-schemas', required : false)

subdir('src')
subdir('data')
subdir('icons')
subdir('po')
subdir('help')

meson.add_install_script('meson_post_install.py')

### SUMMARY PRINTOUT ###

summary({'prefix': ghex_prefix,
        'libdir': ghex_libdir,
        'datadir': ghex_datadir,
        'includedir': ghex_includedir,
        }, section: 'Directories')

summary({'Development build': get_option('development'),
        }, section: 'Configuration')
